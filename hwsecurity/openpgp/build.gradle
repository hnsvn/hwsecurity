apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka-android'

dependencies {
    api project(':hwsecurity:core')
    implementation project(':hwsecurity:provider')

    compileOnly project(':hwsecurity:ui')
    compileOnly 'com.google.android.material:material:1.1.0'

    implementation 'org.bouncycastle:bcprov-jdk15on:1.65'

    compileOnly 'androidx.annotation:annotation:1.1.0'

    api 'com.google.auto.value:auto-value-annotations:1.6.2'
    annotationProcessor 'com.google.auto.value:auto-value:1.6.2'
    annotationProcessor 'com.ryanharter.auto.value:auto-value-parcel:0.2.6'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.2.2'
    testImplementation 'org.mockito:mockito-core:2.18.0'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion 14
        versionName rootProject.ext.hwSdkVersionName
        consumerProguardFiles 'hwsecurity-openpgp.pro'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Do not abort build if lint finds errors
    lintOptions {
        abortOnError false
    }
}

// https://developer.android.com/studio/build/maven-publish-plugin
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'de.cotech'
                artifactId = 'hwsecurity-openpgp'
                version = android.defaultConfig.versionName

                pom {
                    url = 'https://hwsecurity.dev'
                    licenses {
                        license {
                            name = 'Commercial'
                            url = 'https://hwsecurity.dev/sales/'
                            distribution = 'repo'
                        }
                        license {
                            name = 'GNU General Public License, version 3'
                            url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                        }
                    }
                    organization {
                        name = 'Confidential Technologies GmbH'
                        url = 'https://www.cotech.de'
                    }
                }
            }
        }
        /*
         * To upload release, create file gradle.properties in ~/.gradle/ with this content:
         *
         * cotechMavenName=xxx
         * cotechMavenPassword=xxx
         */
        if (project.hasProperty('cotechMavenName') && project.hasProperty('cotechMavenPassword')) {
            println "Found cotechMavenName, cotechMavenPassword in gradle.properties!"

            repositories {
                maven {
                    credentials {
                        username cotechMavenName
                        password cotechMavenPassword
                    }
                    url = "https://maven.cotech.de"
                }
            }
        }
    }
}

dokka {
    moduleName = 'hwsecurity-openpgp'
    outputFormat = "hugo"
    outputDirectory = "$projectDir/../../hwsecurity.dev/content/reference"
    sourceDirs = files('src/main/java')

    packageOptions {
        prefix = "de.cotech.hw.openpgp.internal"
        suppress = true
    }
}
